{
  "nbformat": 4,
  "nbformat_minor": 0,
  "metadata": {
    "colab": {
      "provenance": [],
      "gpuType": "T4",
      "authorship_tag": "ABX9TyNvPi89byw0JIPCv4BhUQqC",
      "include_colab_link": true
    },
    "kernelspec": {
      "name": "python3",
      "display_name": "Python 3"
    },
    "language_info": {
      "name": "python"
    },
    "accelerator": "GPU"
  },
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {
        "id": "view-in-github",
        "colab_type": "text"
      },
      "source": [
        "<a href=\"https://colab.research.google.com/github/IVPITER080306/Dash_Lab_repository_f20241403/blob/main/04_Optimised.ipynb\" target=\"_parent\"><img src=\"https://colab.research.google.com/assets/colab-badge.svg\" alt=\"Open In Colab\"/></a>"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 7,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "0xObDc18ST9I",
        "outputId": "e479ba50-343c-4022-cb5f-c08e3e788e4c"
      },
      "outputs": [
        {
          "output_type": "stream",
          "name": "stdout",
          "text": [
            "Overwriting optimised.cu\n"
          ]
        }
      ],
      "source": [
        "%%writefile optimised.cu\n",
        "#include <stdio.h>\n",
        "#include <cuda_runtime.h>\n",
        "#include <stdlib.h>\n",
        "\n",
        "#define TILE_WIDTH 32\n",
        "\n",
        "#define REG_TILE_WIDTH 4\n",
        "\n",
        "#define BLOCK_COLS 8\n",
        "#define BLOCK_ROWS 32\n",
        "\n",
        "__global__ void tiling_double_buffer(float *a, float *b, float *c, int m, int n, int k)\n",
        "{\n",
        "    // We use two \"pages\" (0 and 1) for shared memory.\n",
        "    // While computing on page 0, we load into page 1, and vice versa.\n",
        "    __shared__ float tile_a[2][TILE_WIDTH][TILE_WIDTH];\n",
        "    __shared__ float tile_b[2][TILE_WIDTH][TILE_WIDTH];\n",
        "\n",
        "    int row = blockIdx.y * blockDim.y + threadIdx.y;\n",
        "    int col = blockIdx.x * TILE_WIDTH + threadIdx.x * REG_TILE_WIDTH;\n",
        "\n",
        "    float sum[REG_TILE_WIDTH];\n",
        "    for (int i = 0; i < REG_TILE_WIDTH; i++) {\n",
        "        sum[i] = 0.0f;\n",
        "    }\n",
        "\n",
        "    int thread_idx_1d = threadIdx.y * blockDim.x + threadIdx.x;\n",
        "    int block_size = blockDim.x * blockDim.y;\n",
        "\n",
        "    int a_start_row = blockIdx.y * TILE_WIDTH;\n",
        "    int b_start_col = blockIdx.x * TILE_WIDTH;\n",
        "\n",
        "    int num_tiles = (k + TILE_WIDTH - 1) / TILE_WIDTH;\n",
        "\n",
        "    // All threads cooperate to load tile 0 into buffer 0.\n",
        "    {\n",
        "        int t = 0; // Tile index\n",
        "        int a_start_col = t * TILE_WIDTH;\n",
        "        int b_start_row = t * TILE_WIDTH;\n",
        "\n",
        "        for (int i = 0; i < 4; i++) { // Each thread loads 4 elements\n",
        "            int tile_idx = thread_idx_1d + i * block_size;\n",
        "            int load_row = tile_idx / TILE_WIDTH; // Row within the 32x32 tile\n",
        "            int load_col = tile_idx % TILE_WIDTH; // Col within the 32x32 tile\n",
        "\n",
        "            // Load tile_a[0]\n",
        "            int a_global_row = a_start_row + load_row;\n",
        "            int a_global_col = a_start_col + load_col;\n",
        "            if (a_global_row < m && a_global_col < k)\n",
        "                tile_a[0][load_row][load_col] = a[a_global_row * k + a_global_col];\n",
        "            else\n",
        "                tile_a[0][load_row][load_col] = 0.0f;\n",
        "\n",
        "            // Load tile_b[0]\n",
        "            int b_global_row = b_start_row + load_row;\n",
        "            int b_global_col = b_start_col + load_col;\n",
        "            if (b_global_row < k && b_global_col < n)\n",
        "                tile_b[0][load_row][load_col] = b[b_global_row * n + b_global_col];\n",
        "            else\n",
        "                tile_b[0][load_row][load_col] = 0.0f;\n",
        "        }\n",
        "    }\n",
        "    __syncthreads(); // Wait for the first tile to be loaded\n",
        "\n",
        "    //Main Loop with Double Buffering\n",
        "    for (int t = 0; t < num_tiles; t++) {\n",
        "\n",
        "        // current_buffer: buffer to compute on (loaded in previous iteration)\n",
        "        // next_buffer: buffer to load into (for next iteration)\n",
        "        int current_buffer = t % 2;\n",
        "        int next_buffer = (t + 1) % 2;\n",
        "\n",
        "        // Start loading the next tile while we compute on the current one.\n",
        "        if (t < num_tiles - 1) {\n",
        "            int next_tile_k_a = (t + 1) * TILE_WIDTH; // Col for A\n",
        "            int next_tile_k_b = (t + 1) * TILE_WIDTH; // Row for B\n",
        "\n",
        "            for (int i = 0; i < 4; i++) { // Each thread loads 4 elements\n",
        "                int tile_idx = thread_idx_1d + i * block_size;\n",
        "                int load_row = tile_idx / TILE_WIDTH;\n",
        "                int load_col = tile_idx % TILE_WIDTH;\n",
        "\n",
        "                // Load tile_a[next_buffer]\n",
        "                int a_global_row = a_start_row + load_row;\n",
        "                int a_global_col = next_tile_k_a + load_col;\n",
        "                if (a_global_row < m && a_global_col < k)\n",
        "                    tile_a[next_buffer][load_row][load_col] = a[a_global_row * k + a_global_col];\n",
        "                else\n",
        "                    tile_a[next_buffer][load_row][load_col] = 0.0f;\n",
        "\n",
        "                // Load tile_b[next_buffer]\n",
        "                int b_global_row = next_tile_k_b + load_row;\n",
        "                int b_global_col = b_start_col + load_col;\n",
        "                if (b_global_row < k && b_global_col < n)\n",
        "                    tile_b[next_buffer][load_row][load_col] = b[b_global_row * n + b_global_col];\n",
        "                else\n",
        "                    tile_b[next_buffer][load_row][load_col] = 0.0f;\n",
        "            }\n",
        "        }\n",
        "\n",
        "        // Compute using the data in the current_buffer\n",
        "        for (int i = 0; i < TILE_WIDTH; i++) {\n",
        "            float a_reg = tile_a[current_buffer][threadIdx.y][i];\n",
        "\n",
        "            for (int j = 0; j < REG_TILE_WIDTH; j++) {\n",
        "                int b_col_idx = threadIdx.x * REG_TILE_WIDTH + j;\n",
        "                float b_reg = tile_b[current_buffer][i][b_col_idx];\n",
        "                sum[j] += a_reg * b_reg;\n",
        "            }\n",
        "        }\n",
        "\n",
        "        // This barrier ensures two things:\n",
        "        // 1. The computation on tile t is finished.\n",
        "        // 2. The loading of tile t+1 is finished.\n",
        "        // Now we are ready to loop again, swapping buffers.\n",
        "        __syncthreads();\n",
        "    }\n",
        "\n",
        "    for (int j = 0; j < REG_TILE_WIDTH; j++) {\n",
        "        int output_col = col + j;\n",
        "        if (row < m && output_col < n)\n",
        "            c[row * n + output_col] = sum[j];\n",
        "    }\n",
        "}\n",
        "\n",
        "\n",
        "void init_matrix(float *mat, int rows, int cols) {\n",
        "    for (int i = 0; i < rows * cols; i++) {\n",
        "        mat[i] = (float)rand() / RAND_MAX;\n",
        "    }\n",
        "}\n",
        "\n",
        "int main()\n",
        "{\n",
        "    const int M = 1024;\n",
        "    const int K = 1024;\n",
        "    const int N = 1024;\n",
        "\n",
        "    cudaEvent_t start, stop;\n",
        "    cudaEventCreate(&start);\n",
        "    cudaEventCreate(&stop);\n",
        "\n",
        "    float *h_a, *h_b, *h_c_gpu;\n",
        "    float *d_a, *d_b, *d_c;\n",
        "    size_t size_a = M * K * sizeof(float);\n",
        "    size_t size_b = K * N * sizeof(float);\n",
        "    size_t size_c = M * N * sizeof(float);\n",
        "    h_a = (float *)malloc(size_a);\n",
        "    h_b = (float *)malloc(size_b);\n",
        "    h_c_gpu = (float *)malloc(size_c);\n",
        "    init_matrix(h_a, M, K);\n",
        "    init_matrix(h_b, K, N);\n",
        "    cudaMalloc((void **)&d_a, size_a);\n",
        "    cudaMalloc((void **)&d_b, size_b);\n",
        "    cudaMalloc((void **)&d_c, size_c);\n",
        "    cudaMemcpy(d_a, h_a, size_a, cudaMemcpyHostToDevice);\n",
        "    cudaMemcpy(d_b, h_b, size_b, cudaMemcpyHostToDevice);\n",
        "\n",
        "    dim3 block(BLOCK_COLS, BLOCK_ROWS); // (8X32)\n",
        "    dim3 grid((N + TILE_WIDTH - 1) / TILE_WIDTH, (M + TILE_WIDTH - 1) / TILE_WIDTH);\n",
        "\n",
        "    // Warm-up run\n",
        "    tiling_double_buffer<<<grid, block>>>(d_a, d_b, d_c, M, N, K);\n",
        "    cudaDeviceSynchronize();\n",
        "\n",
        "\n",
        "    int iterations = 100;\n",
        "    float total_time = 0.0f;\n",
        "\n",
        "    for (int i = 0; i < iterations; i++) {\n",
        "\n",
        "        cudaEventRecord(start);\n",
        "\n",
        "        tiling_double_buffer<<<grid, block>>>(d_a, d_b, d_c, M, N, K);\n",
        "\n",
        "        cudaEventRecord(stop);\n",
        "        cudaEventSynchronize(stop);\n",
        "\n",
        "        float iter_time;\n",
        "        cudaEventElapsedTime(&iter_time, start, stop);\n",
        "        total_time += iter_time;\n",
        "    }\n",
        "    printf(\"Average kernel execution time: %f ms\\n\", total_time / iterations);\n",
        "    printf(\"Average GFLOPS: %f\\n\", 2.0f * N * M * K / (total_time / iterations) / 1e6);\n",
        "\n",
        "\n",
        "    cudaEventDestroy(start);\n",
        "    cudaEventDestroy(stop);\n",
        "    cudaMemcpy(h_c_gpu, d_c, size_c, cudaMemcpyDeviceToHost);\n",
        "    cudaFree(d_a);\n",
        "    cudaFree(d_b);\n",
        "    cudaFree(d_c);\n",
        "    free(h_a);\n",
        "    free(h_b);\n",
        "    free(h_c_gpu);\n",
        "    return 0;\n",
        "}"
      ]
    },
    {
      "cell_type": "code",
      "source": [
        "!nvcc -arch=sm_75 optimised.cu -o optimised\n",
        "!./optimised"
      ],
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "CkykmPkPSddi",
        "outputId": "26927dfe-a245-4a8f-e75e-087fdaf361cd"
      },
      "execution_count": 8,
      "outputs": [
        {
          "output_type": "stream",
          "name": "stdout",
          "text": [
            "Average kernel execution time: 2.930012 ms\n",
            "Average GFLOPS: 732.926592\n"
          ]
        }
      ]
    }
  ]
}